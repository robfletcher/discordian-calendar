apply plugin: "maven-publish"
apply plugin: "com.jfrog.bintray"
apply from: "$rootDir/gradle/credentials.gradle"

task sourceJar(type: Jar) {
  description "An archive of the source code"
  classifier "sources"
  from sourceSets.main.allJava, sourceSets.main.resources
}

task javadocJar(type: Jar) {
  description "An archive of the JavaDocs"
  classifier "javadoc"
  from javadoc
}

artifacts {
  archives sourceJar, javadocJar
}

publishing {
  publications {
    mavenJava(MavenPublication) {
      from components.java

      artifact sourceJar {
        classifier "sources"
      }

      artifact javadocJar {
        classifier "javadoc"
      }
    }
  }
}

bintray {
  user = bintrayUsername
  key = bintrayApiKey
  publications = ["mavenJava"]
  publish = true
  pkg {
    repo = "maven"
    name = "discordian-calendar"
    desc = "An implementation of the Discordian calendar for the Java 8 date/time API"

    websiteUrl = "https://github.com/robfletcher/discordian-calendar"
    issueTrackerUrl = "https://github.com/robfletcher/discordian-calendar/issues"
    vcsUrl = "https://github.com/robfletcher/discordian-calendar.git"
    licenses = ["WTFPL"]
    labels = ["date", "time", "datetime"]
    publicDownloadNumbers = true
    version {
      name = project.version
      vcsTag = project.version
    }
  }
}
